;;; me-builtin.el --- Emacs built-in packages -*- lexical-binding: t; -*-

;; Copyright (C) 2022-2026  Abdelhak Bougouffa

;; Author: Abdelhak Bougouffa (rot13 "nobhtbhssn@srqbencebwrpg.bet")
;; Created: 2023-03-26
;; Last modified: 2026-01-16

;;; Commentary:

;;; Code:

(require 'me-lib)

(use-package emacs
  :hook
  (after-save . +save-guess-file-mode-h)
  (minibuffer-setup . cursor-intangible-mode) ; See the `minibuffer-prompt-properties' below
  (minemacs-first-file . +sudo-indicator-mode)
  :custom
  (auto-save-list-file-prefix (+directory-ensure minemacs-local-dir "auto-save/"))
  (auto-save-file-name-transforms ; Set file naming transform for `auto-save'
   `(("\\`/[^/]*:\\([^/]*/\\)*\\([^/]*\\)\\'" ,(concat auto-save-list-file-prefix "tramp-\\2") sha1)
     (".*" ,auto-save-list-file-prefix sha1)))
  (scroll-preserve-screen-position t) ; Keep the point in the same position while scrolling
  (scroll-conservatively 101) ; Do not move cursor to the center when scrolling
  (scroll-margin 2) ; Scroll at a margin of one line
  (create-lockfiles nil) ; Disable lockfiles
  (make-backup-files nil) ; Disable making backup files
  (tab-always-indent 'complete) ; Make TAB indents first, then inserts the TAB character
  (require-final-newline t) ; End files with newline
  (undo-limit 2000000) ; 2MB, soft limit for undo data (per-buffer, size of `buffer-undo-list') to keep (def. 160kB)
  (undo-strong-limit 5000000) ; 5MB, discard undo data greater than this generated by a single a command, except the last one (def. 240kB)
  (undo-outer-limit 30000000) ; 30MB, like the strong limit, but including the last command, prints a warning when it happens (def. 24MB)
  (kill-ring-max 1000) ; Maximum length of kill ring before oldest elements are thrown away (def. 120)
  (use-system-tooltips nil) ; Use small frames to display tooltips instead of the default OS tooltips
  (window-combination-resize t) ; Resize window combinations proportionally
  (x-stretch-cursor t) ; Stretch cursor to the glyph width
  (frame-resize-pixelwise t) ; Do force frame size to be a multiple of char size
  (frame-title-format '("GNU Emacs (%b)")) ; Custom frame title
  (read-process-output-max ; Increase single chunk bytes to read from subprocess (def. 4096)
   (condition-case nil
       (with-temp-buffer ; On GNU/Linux systems, the value should not exceed `pipe-max-size'
         (insert-file-contents "/proc/sys/fs/pipe-max-size")
         (string-to-number (buffer-string)))
     (error (* 1024 1024))))
  (read-buffer-completion-ignore-case t)
  (read-file-name-completion-ignore-case t) ; Ignores case when completing files names
  (read-extended-command-predicate #'command-completion-default-include-p) ; In `M-x', hide commands not relevant for the current mode
  (completions-sort (if (>= emacs-major-version 30) 'historical 'alphabetical))
  (enable-recursive-minibuffers t) ; Enable recursive calls to minibuffer
  (minibuffer-prompt-properties ; Do not allow the cursor in the minibuffer prompt (works with `cursor-intangible-mode')
   '(read-only t cursor-intangible t face minibuffer-prompt))
  (sentence-end-double-space nil) ; Use single space between sentences
  (delete-by-moving-to-trash t) ; Move stuff to trash
  (save-some-buffers-default-predicate #'save-some-buffers-root) ; Save files only in sub-directories of current project
  (inhibit-startup-screen t) ; Inhibit startup message
  (large-file-warning-threshold (* 20 1024 1024)) ; Increase the large file threshold to 20MiB (10MB)
  (use-dialog-box nil) ; Always prompt in minibuffer (no GUI)
  (use-short-answers t) ; Use y or n instead of yes or no
  (confirm-kill-emacs #'y-or-n-p) ; Confirm before quitting
  (prettify-symbols-unprettify-at-point t) ; Show unprettified symbol under cursor (when in `prettify-symbols-mode')
  (display-fill-column-indicator-character ?\u250a) ; Use a dashed line for `display-fill-column-indicator-mode'
  (apropos-do-all t) ; Make apropos commands search more extensively
  (shell-kill-buffer-on-exit t) ; Kill the shell buffer after exit
  (widget-image-enable nil) ; No ugly button for widgets
  (tooltip-hide-delay 20) ; Make tooltips last a bit longer (default 10s)
  (image-animate-loop t) ; Animated images loop forever instead of playing the animation only once
  (icomplete-compute-delay 0.01) ; Don't delay displaying completion candidates in `fido-mode' (def. 0.15)
  (inhibit-compacting-font-caches t) ; Trade memory usage for less lagging
  (set-mark-command-repeat-pop t) ; Use C-u C-SPC C-SPC... instead of C-u C-SPC C-u C-SPC...
  (mode-line-collapse-minor-modes t) ; Collapse minor modes in mode line lighters (Emacs 31+)
  (password-cache-expiry 60) ; Cache for one minute (def. 16s)
  (epg-pinentry-mode 'loopback) ; Force gpg-agent to use minibuffer to prompt for passphrase (GPG 2.1+).
  (doc-view-continuous t)
  (vc-follow-symlinks t)
  (initial-major-mode 'fundamental-mode)
  :init
  (setq-default fill-column 80)

  ;; When `me-completion/vertico' is disabled, enable `fido-vertical-mode' as a fallback
  (when (+package-disabled-p 'vertico 'me-completion)
    (fido-vertical-mode 1))

  ;; When `me-completion/corfu' is disabled, enable `global-completion-preview-mode'
  (when (and (fboundp 'global-completion-preview-mode) (+package-disabled-p 'corfu 'me-completion))
    (global-completion-preview-mode 1)
    (add-hook 'minibuffer-mode-hook 'completion-preview-mode)) ; Use also in the minibuffer

  ;; Prefer `embark', see https://www.matem.unam.mx/~omar/apropos-emacs.html#the-case-against-which-key-a-polemic
  (when (+package-disabled-p 'embark 'me-completion)
    (which-key-mode 1))

  (eval-after-load 'minemacs-first-file (+shutup! (epa-file-enable)))

  ;; Replace some keybindings (like `C-x C-f', `C-x C-d', etc.) with `ffap's ones
  (ffap-bindings)

  ;; Inhibit startup message and screen
  (fset 'display-startup-echo-area-message #'ignore)
  (fset 'display-startup-screen #'ignore)

  ;;; Why use anything but UTF-8?
  (prefer-coding-system 'utf-8)
  (set-charset-priority 'unicode)
  (set-default-coding-systems 'utf-8)
  (set-language-environment "UTF-8")
  (set-locale-environment "en_US.UTF-8")
  ;; Use UTF-16-LE in Windows, see: https://rufflewind.com/2014-07-20/pasting-unicode-in-emacs-on-windows
  (set-selection-coding-system (if (featurep 'os/win) 'utf-16-le 'utf-8))
  :config
  ;; Unbind some annoying commands
  (keymap-global-unset "C-z" 'remove) ; `suspend-frame'
  (keymap-global-unset "C-x C-z" 'remove)

  ;; Remap some keys/page
  (keymap-global-set "M-:" #'pp-eval-expression) ; Instead of `eval-expression'
  (keymap-global-set "C-c f" #'recentf) ; Instead of `find-file-read-only'
  (keymap-global-set "<f1>" #'ansi-term) ; Instead of `help-map' (accessible via `C-h')
  (keymap-global-set "C-c D" #'+delete-current-file-and-buffer)
  (keymap-global-set "<remap> <kill-word>" #'+kill-whitespace-or-word) ; M-d
  (keymap-global-set "<remap> <kill-region>" #'+kill-region-or-backward-word) ; C-w
  (keymap-global-set "<remap> <backward-kill-word>" #'+backward-kill-whitespace-or-word) ; M-delete or C-backspace

  ;; Save pressing Shift when entering numbers on AZERTY keyboards
  (with-eval-after-load 'window
    (keymap-set ctl-x-map "à" #'delete-window)            ; C-x 0
    (keymap-set ctl-x-map "&" #'delete-other-windows)     ; C-x 1
    (keymap-set ctl-x-map "é" #'split-window-below)       ; C-x 2
    (keymap-set ctl-x-map "\"" #'split-window-right)      ; C-x 3
    (keymap-set ctl-x-map "'" ctl-x-4-map)                ; C-x 4
    (keymap-set ctl-x-4-map "à" #'kill-buffer-and-window) ; C-x 4 0
    (keymap-set ctl-x-4-map "&" #'same-window-prefix)     ; C-x 4 1
    (keymap-set ctl-x-4-map "'" #'other-window-prefix))   ; C-x 4 4

  (with-eval-after-load 'prog-mode
    (keymap-set prog-mode-map "C-<down-mouse-1>" #'mouse-drag-region)
    (keymap-set prog-mode-map "C-<mouse-1>" #'xref-find-definitions-at-mouse))

  (with-eval-after-load 'help
    (keymap-set help-map "h" #'+describe-at-point))

  ;; Enable some useful Emacs commands by default
  (dolist (command '(list-timers narrow-to-region narrow-to-defun narrow-to-page upcase-region downcase-region))
    (put command 'disabled nil))

  (defvar-keymap minemacs-prefix-map
    :doc "MinEmacs' prefix, under `C-z'."
    :name "MinEmacs' prefix"
    "b f" #'follow-mode
    "b x" #'bury-buffer)

  (keymap-global-set "C-z" `("minemacs-prefix" . ,minemacs-prefix-map))

  (defvar-keymap minemacs-open-thing-map :doc "Open/toggle thing, under `C-c o'." :name "Open/toggle thing")
  (keymap-global-set "C-c o" `("open-thing" . ,minemacs-open-thing-map))
  (keymap-set minemacs-open-thing-map "o" #'+open-with)

  ;; Disable previously enabled custom themes before enabling a new one.
  (defun +theme--disable-previous-themes:before-a (&rest _args)
    (mapc #'disable-theme custom-enabled-themes))
  (advice-add 'load-theme :before '+theme--disable-previous-themes:before-a)

  ;; Show trailing whitespace in `prog-mode', `conf-mode' and `diff-mode'
  (+setq-hook! (prog-mode conf-mode diff-mode) show-trailing-whitespace t)

  ;; Don't mess up with whitespaces in `diff-mode', they are part of the syntax
  (add-hook 'diff-mode-hook (lambda () (electric-indent-local-mode -1)))

  ;; Subtle color for trailing whitespaces
  (defun +face--subtle-trailing-whitespace-h (&rest _args)
    (with-eval-after-load 'faces
      (when (display-graphic-p)
        (when-let* ((new-color (+color-subtle 'trailing-whitespace 50)))
          (set-face-background 'trailing-whitespace new-color)))))

  (dolist (hook '(enable-theme-functions disable-theme-functions server-after-make-frame-hook))
    (add-hook hook #'+face--subtle-trailing-whitespace-h))

  ;; By default, Emacs asks before quitting with "C-x C-c", but when running an
  ;; Emacs Client session, it won't ask unless a file is not saved. I hit "C-x
  ;; C-c" a lot by error, so lets make it ask before quitting.
  (defun +save-buffers-kill-terminal--ask-on-emacsclient:around-a (origfn arg)
    (if (frame-parameter nil 'client)
        (pcase (read-answer "Quit Emacs? " '(("yes" ?y "quit Emacs Client") ("no" ?n "don't quit") ("quit" ?q "kill Emacs")))
          ("yes" (apply origfn arg))
          ("quit" (kill-emacs)))
      (apply origfn arg)))

  (advice-add 'save-buffers-kill-terminal :around #'+save-buffers-kill-terminal--ask-on-emacsclient:around-a)

  ;; Guess the major mode after saving a file in `fundamental-mode' (adapted from Doom Emacs).
  (defun +save-guess-file-mode-h ()
    (when (eq major-mode 'fundamental-mode)
      (let ((buffer (or (buffer-base-buffer) (current-buffer))))
        (and (buffer-file-name buffer)
             (eq buffer (window-buffer (selected-window))) ; Only when the buffer is in the current window
             (set-auto-mode)))))

  ;; Offer to create parent directories if they do not exist. The arguments are
  ;; made optional so we can use it with both `find-file' and `rename-file'
  (defun +create-non-existent-directory (&optional _filename newname &rest _args)
    (when-let* ((target-file (or newname buffer-file-name))
                (parent-directory (file-name-directory target-file))
                ((not (file-exists-p parent-directory)))
                ((y-or-n-p (format "Directory %S does't exist! Create it?" parent-directory))))
      (make-directory parent-directory t)
      t))

  (advice-add 'copy-file :before #'+create-non-existent-directory)
  (advice-add 'rename-file :before #'+create-non-existent-directory)
  (add-hook 'find-file-not-found-functions #'+create-non-existent-directory 80))

(use-package transient
  :straight (:host github :repo "magit/transient")) ; Magit depends on the fresh version

(use-package newcomment
  :custom
  (comment-multi-line t)
  :init
  (+setq-hook! python-base-mode comment-inline-offset 2)) ; Python's PEP8 recommends two spaces

(use-package crm
  :custom
  (crm-prompt "[CRM%s] %p")
  :config
  (unless (>= emacs-major-version 31) ; Backporting Emacs 31's `crm-prompt' integration
    (defvar crm-prompt "[CRM%s] %p")
    (defun +crm--indicator:filter-args-a (args)
      (cons (format-spec
             crm-prompt
             (let* ((prompt (car args))
                    (sep (or (get-text-property 0 'separator crm-separator) (string-replace "[ \t]*" "" crm-separator)))
                    (desc (or (get-text-property 0 'description crm-separator) (concat "list separated by " sep))))
               `((?s . ,sep) (?d . ,desc) (?p . ,prompt))))
            (cdr args)))
    (advice-add #'completing-read-multiple :filter-args #'+crm--indicator:filter-args-a)))

(use-package tramp
  :custom
  (tramp-backup-directory-alist backup-directory-alist)
  ;; TRAMP optimizations from: https://coredumped.dev/2025/06/18/making-tramp-go-brrrr./
  (tramp-use-scp-direct-remote-copying t)
  (tramp-copy-size-limit (* 1024 1024)) ; 1MB (def. 10kB)
  (tramp-verbose (if minemacs-verbose-p 4 2))
  (remote-file-name-inhibit-locks t)
  (tramp-save-ad-hoc-proxies t)
  :config
  (connection-local-set-profile-variables
   'remote-direct-async-process
   '((tramp-direct-async-process . t)))

  (connection-local-set-profiles
   '(:application tramp :protocol "scp")
   'remote-direct-async-process)

  (setq tramp-temp-name-prefix (expand-file-name "tramp." temporary-file-directory)
        ;; PERF: More responsive file editing via TRAMP.
        ;; See: "(tramp) Ssh setup" and https://news.ycombinator.com/item?id=39193252
        tramp-ssh-controlmaster-options
        (concat
         "-o ControlPath=" (file-name-as-directory temporary-file-directory) "tramp.ssh-controlpath-%%C "
         "-o ControlMaster=auto -o ControlPersist=1800 " ; persist for 30min
         "-o ServerAliveInterval=5 -o ServerAliveCountMax=2")))

(use-package dired
  ;; Enable adding mail attachments from dired "C-c RET C-a" for
  ;; `gnus-dired-attach'
  :hook
  (dired-mode . turn-on-gnus-dired-mode)
  (dired-mode . dired-hide-details-mode)
  :custom
  (dired-dwim-target t)
  (dired-listing-switches "-AGFhlv --group-directories-first --time-style=long-iso")
  (dired-auto-revert-buffer t)
  (dired-movement-style 'cycle)
  (dired-recursive-copies 'always)
  (dired-recursive-deletes 'top)
  (dired-clean-confirm-killing-deleted-buffers nil))

(use-package dired-aux
  :custom
  (dired-vc-rename-file t)
  (dired-create-destination-dirs 'ask)
  :config
  (cl-callf append dired-compress-files-alist
    '(("\\.tgz\\'" . "tar -cf - %i | gzip -c9 > %o")
      ("\\.tar\\'" . "tar -cf %o %i"))))

(use-package dired-x
  :hook (dired-mode . dired-omit-mode)
  :custom
  (dired-omit-verbose nil))

(use-package project
  :commands (project-remember-projects-under)
  :hook (kill-emacs . +project-forget-zombie-projects)
  :custom
  (project-switch-use-entire-map t) ; Provide all `project-prefix-map' commands when switching projects
  (project-vc-extra-root-markers '(".projectile.el" ".project.el" ".project" ".repo" "*.csproj" "*.fsproj" "*.vbproj" "*.vcxproj" "*.vdproj" "*.sln" ".code-workspace"))
  (project-vc-ignores '(".ccls-cache/" ".cache/" ".cppcheck-build/")))

(use-package tab-bar
  :hook (minemacs-lazy . tab-bar-mode)
  :custom
  (tab-bar-format '(tab-bar-format-history tab-bar-format-tabs tab-bar-separator))
  (tab-bar-tab-name-format-function #'+tab-bar--tab-spaced-name-format)
  (tab-bar-close-button-show nil)
  (tab-bar-auto-width nil)
  (tab-bar-show t))

(use-package editorconfig
  :hook (minemacs-first-file . editorconfig-mode)
  :init
  (add-hook 'prog-mode-hook #'+editorconfig-guess-style-from-clang-format 80)
  :config
  (add-to-list 'editorconfig-indentation-alist '(protobuf-ts-mode . protobuf-ts-mode-indent-offset)))

(use-package flymake
  :hook ((prog-mode conf-mode) . flymake-mode)
  :custom
  (flymake-fringe-indicator-position 'right-fringe)
  (flymake-margin-indicator-position 'right-margin) ; Added in Emacs 30 (see `flymake-indicator-type')
  :config
  (with-eval-after-load 'elisp-mode ; Use the session's `load-path' with `flymake'
    (cl-callf append elisp-flymake-byte-compile-load-path load-path)))

(use-package eshell
  :hook (eshell-mode . minemacs-reduce-font-size)
  :custom
  (eshell-scroll-to-bottom-on-input 'this))

(use-package reftex ;; Inspired by Doom Emacs
  :hook (reftex-toc-mode . reftex-toc-rescan)
  :custom
  ;; Get RefTeX working with BibLaTeX. See: https://tex.stackexchange.com/a/31992/43165
  (reftex-cite-format
   '((?a . "\\autocite[]{%l}")
     (?b . "\\blockcquote[]{%l}{}")
     (?c . "\\cite[]{%l}")
     (?f . "\\footcite[]{%l}")
     (?n . "\\nocite{%l}")
     (?p . "\\parencite[]{%l}")
     (?s . "\\smartcite[]{%l}")
     (?t . "\\textcite[]{%l}"))
   ;; This is needed when `reftex-cite-format' is set. See: https://superuser.com/a/1386206
   (reftex-plug-into-AUCTeX t)
   (reftex-toc-split-windows-fraction 0.3)))

(use-package bibtex
  :hook (bibtex-mode . display-line-numbers-mode)
  :custom
  (bibtex-dialect 'biblatex)
  (bibtex-align-at-equal-sign t)
  (bibtex-text-indentation 20))

(use-package treesit
  :when (featurep 'feat/tree-sitter)
  :hook (treesit--explorer-tree-mode . show-paren-local-mode)
  :mode ("/Cargo\\.lock\\'" . toml-ts-mode)
  :mode ((rx (any ?. ?_) (or "clang-format" "clang-tidy") eol) . yaml-ts-mode)
  :custom
  (treesit-font-lock-level 4)
  ;; BUG+TEMP: Editing YAML files in `yaml-ts-mode' cause Emacs to crash, especially when the file contain invalid YAML
  (treesit-enabled-modes (delq 'yaml-ts-mode (seq-uniq (mapcar #'cdr treesit-major-mode-remap-alist)))))

(use-package markdown-ts-mode
  :when (and (featurep 'feat/tree-sitter) (>= emacs-major-version 31)) ; Built-in in Emacs 31+
  :config
  (cl-callf append markdown-ts--code-block-language-map
    '(("emacs-lisp" . elisp)
      ("shell" . bash)))
  (cl-callf append markdown-ts-code-block-source-mode-map
    '((elisp . emacs-lisp-mode)
      (lisp . lisp-mode)
      (scheme . scheme-mode))))

(use-package autoinsert
  :custom
  (auto-insert-directory (+directory-ensure minemacs-local-dir "auto-insert/")))

(use-package paren
  :custom
  (show-paren-when-point-inside-paren t) ; Highlight when the point is inside the parentheses
  (show-paren-when-point-in-periphery t)) ; Highlight when the point in the periphery of the parentheses

(use-package align
  :bind (("M-[" . +align-code)
         ("C-c [" . align-regexp))
  :config
  ;; Add more C-like modes to the list
  (cl-callf append align-c++-modes '(csharp-mode cuda-mode opencl-c-mode llvm-ts-mode))
  (defun +align-code (beg end &optional arg)
    "Like `align', but indents if called with prefix."
    (interactive "rP")
    (if arg
        (let ((end-mark (copy-marker end)))
          (indent-region beg end-mark nil)
          (align beg end-mark))
      (align beg end))))

(use-package hideif
  :custom
  (hide-ifdef-shadow t)
  (hide-ifdef-initially t)
  :hook (hide-ifdef-mode . +hide-ifdef-get-env-from-compilation-db))

(use-package hl-line
  :hook ((prog-mode conf-mode text-mode dired-mode profiler-report-mode) . hl-line-mode)) ; Highlight the current line

(use-package cc-vars
  :config
  (mapc (lambda (m) (setq-default c-default-style (+alist-set (car m) (cdr m) c-default-style)))
        '((c-mode . "k&r") (c++-mode . "k&r"))))

(use-package cc-mode
  :mode ("\\.c\\.[Gg][Ee][Nn]]\\'" . c-mode)) ; Some automatically generated C files have this format

(use-package c-ts-mode
  :when (featurep 'feat/tree-sitter)
  :custom
  (c-ts-mode-indent-style 'k&r)
  (c-ts-mode-enable-doxygen t))

(use-package python
  :custom
  (python-shell-dedicated 'project)
  (python-indent-guess-indent-offset-verbose nil))

(use-package modula2
  :custom
  (m2-compile-command "gm2 -fiso") ; Use GNU GCC Modula-2 compiler with ISO dialect
  :mode ("\\.m2\\'" . m2-mode)
  :init
  (add-to-list 'magic-mode-alist '(+m2-definition-p . m2-mode))
  (defun +m2-definition-p ()
    (when-let* ((fname (buffer-file-name))
                (ext (file-name-extension fname))
                ((equal (downcase ext) "def")))
      (save-excursion
        (goto-char (point-min))
        (re-search-forward "DEFINITION[[:space:]]*MODULE[[:space:]]*[[:alnum:]_]*[[:space:]]*;" nil t)))))

(use-package conf-mode
  :mode "\\.prop\\'"
  :mode "\\.rc\\'"
  :mode "/\\.style.\\yapf\\'")

(use-package hideshow
  :hook ((prog-mode conf-mode nxml-mode) . +hs-minor-mode-maybe) ; Hide/show code blocks, a.k.a. code folding
  :custom
  (hs-hide-comments-when-hiding-all nil)
  :bind (:map hs-minor-mode-map
              ("C-c @ C-f"     . +hs-toggle-all)
              ;; The key "à = @" in AZERTY keyboards, the @ need "Alt Gr" to insert it
              ("C-c C-à C-f"   . +hs-toggle-all)
              ("C-c C-à C-h"   . hs-hide-block)
              ("C-c C-à C-s"   . hs-show-block)
              ("C-c C-à C-M-h" . hs-hide-all)
              ("C-c C-à C-M-s" . hs-show-all)
              ("C-c C-à C-l"   . hs-hide-level)
              ("C-c C-à C-c"   . hs-toggle-hiding)
              ("C-c C-à C-a"   . hs-show-all)
              ("C-c C-à C-t"   . hs-hide-all)
              ("C-c C-à C-d"   . hs-hide-block)
              ("C-c C-à C-e"   . hs-toggle-hiding))
  :init
  (defun +hs-minor-mode-maybe () ; Fail sailently
    (condition-case err (hs-minor-mode 1) (error (+log! "`hs-minor-mode': %s" (error-message-string err)))))
  :config
  (defun +hs-toggle-all ()
    (interactive)
    (if (cl-some (+apply-partially-right #'overlay-get 'hs) (overlays-in (point-min) (point-max)))
        (hs-show-all)
      (hs-hide-all))))

(use-package xref
  :hook (xref--xref-buffer-mode . minemacs-reduce-font-size)
  :custom
  ;; Use completion in the minibuffer instead of definitions buffer
  (xref-show-definitions-function #'xref-show-definitions-completing-read)
  ;; Show references in a separate buffer, this is more convenient especially in big codebases
  (xref-show-xrefs-function #'xref-show-definitions-buffer)
  (xref-prompt-for-identifier
   '(not xref-find-definitions xref-find-definitions-other-window xref-find-definitions-other-frame ; The default value
         xref-find-references)) ; We add `xref-find-references', which causes problems on big codebases
  ;; NOTE: Usually, this shorcut can be bound to moves the window (set by the OS
  ;; window manager), so we need to disable it in the WM for this to work.
  :bind ("M-<down-mouse-1>" . xref-find-references-at-mouse)
  :config
  (+setq-hook! xref--xref-buffer-mode truncate-lines t))

(use-package eglot
  :custom
  (eglot-autoshutdown t) ; shutdown after closing the last managed buffer
  (eglot-sync-connect 0) ; async, do not block
  (eglot-report-progress nil) ; disable annoying messages in echo area!
  (eglot-stay-out-of '(yasnippet))
  (eglot-advertise-cancellation t)
  (eglot-ignored-server-capabilities '(:semanticTokensProvider)) ; Tree-sitter does a good job at semantic fontifying
  :config
  ;; Showing pending requests are just visual pollution of the mode-line
  (cl-callf2 remq 'eglot-mode-line-pending-requests eglot-mode-line-format)

  ;; PERF: Optimization, inspired by: https://reddit.com/r/emacs/comments/1gv556t/comment/lxzbfw8
  (unless minemacs-debug-p
    (cl-callf plist-put eglot-events-buffer-config :size 0) ; Disable logs in `eglot-events-buffer' (def. 2000000)
    (with-eval-after-load 'jsonrpc ; Disable logging in `jsonrpc'
      (fset 'jsonrpc--log-event #'ignore)
      (remove-hook 'jsonrpc-event-hook 'jsonrpc--log-event)))

  ;; Don't spit messages to the echo area
  (advice-add 'jsonrpc--message :around #'+apply-suppress-messages)
  (advice-add 'eglot--message :around #'+apply-suppress-messages)

  ;; When a sub/super project with a separate Python virtual environment is detected,
  ;; limit to this one.
  ;; See: https://lists.gnu.org/archive/html/emacs-devel/2022-11/msg01087.html
  (defvar +python-virtualenv-dir-names '(".virtualenv" ".venv"))
  (defun +project-find-virtualenv-for-eglot (dir)
    (when (and (bound-and-true-p eglot-lsp-context) (derived-mode-p 'python-mode))
      (when-let* ((root (cl-some (apply-partially #'locate-dominating-file dir) +python-virtualenv-dir-names)))
        (cons 'transient root))))

  (add-hook 'project-find-functions #'+project-find-virtualenv-for-eglot))

(use-package imenu
  :custom
  (imenu-max-item-length 120)) ; Show longer definitions (def. 60)

(use-package eldoc
  :custom
  (eldoc-documentation-strategy #'eldoc-documentation-compose))

(use-package comint
  :hook (comint-mode . minemacs-reduce-font-size)
  :custom
  (comint-scroll-to-bottom-on-input 'this) ; Move to bottom on input in the current window
  (comint-scroll-to-bottom-on-output 'this) ; Move to bottom on output in the current window
  (comint-buffer-maximum-size (* 64 1024)) ; Increase the maximum buffer size (def. 1024)
  (comint-prompt-read-only t)
  (comint-input-ring-size 5000)) ; Increase the size of the input history ring (def. 500)

(use-package compile
  :hook
  (compilation-filter . ansi-color-compilation-filter) ; Enable ANSI colors in compilation buffer
  (shell-mode . compilation-shell-minor-mode)
  (compilation-mode . minemacs-reduce-font-size)
  :custom
  (compilation-scroll-output t) ; Keep scrolling the compilation buffer, `first-error' can be interesting
  (compilation-always-kill t) ; Always kill current compilation process before starting a new one
  (compilation-skip-visited t) ; Skip visited messages on compilation motion commands
  (compilation-window-height 12) ; Keep it readable
  (compilation-buffer-name-function #'+compilation--project-prefix-buffer-name)
  :config
  ;; Integration of `compile' with `savehist'
  (with-eval-after-load 'savehist
    (add-to-list 'savehist-additional-variables 'compile-history))
  ;; When compiling in a super-project or a project, use its name as a prefix
  (defun +compilation--project-prefix-buffer-name (name-of-mode)
    (if-let* ((default (compilation--default-buffer-name name-of-mode))
              (proj (or (+super-project-current) (project-current)))
              (proj (project-name proj))
              (proj-prefix (concat "*" proj "::"))
              ((not (string-prefix-p proj-prefix default))))
        (concat proj-prefix (string-remove-prefix "*" default))
      default)))

(use-package nxml-mode
  :mode "\\.xmpi\\'"
  :hook (nxml-mode . sgml-electric-tag-pair-mode)) ; Auto rename matching tags

(use-package elisp-mode
  :after minemacs-first-elisp-file ; prevent elisp-mode from being loaded too early
  :custom-face ; better the default cyan color!
  (elisp-shorthand-font-lock-face ((t :inherit font-lock-keyword-face :foreground "red")))
  :custom
  (trusted-content
   (when-let* ((proj (project-current nil minemacs-root-dir))
               (default-directory (project-root proj)))
     (mapcar #'expand-file-name (seq-filter (apply-partially #'string-suffix-p ".el") (project-files proj))))))

(use-package gdb-mi
  :custom
  (gdb-show-main t) ; display source file containing main routine at startup
  (gdb-many-windows t) ; start in gdb-many-windows mode
  (gdb-debug-log-max 1024) ; default 128
  (gdb-restore-window-configuration-after-quit t)
  (gdb-thread-buffer-verbose-names nil)
  (gdb-max-source-window-count 1) ; IDEA: maybe increase it!
  (gdb-display-io-buffer nil) ; don't display a separate IO buffer (Emacs 30.1+)
  (gdb-display-io-nopopup nil)) ; in case we enabled the IO buffer, we don't want it to popup when hidden

(use-package org
  :defer 10
  :preface
  ;; Set to the default value, this can be overwritten in the user config file.
  (setq org-directory minemacs-default-org-dir)
  :custom
  (org-persist-directory (+directory-ensure minemacs-cache-dir "org/persist/"))
  (org-preview-latex-image-directory (+directory-ensure minemacs-cache-dir "org/preview/latex-image/"))
  (org-publish-timestamp-directory (+directory-ensure minemacs-cache-dir "org/publish/timestamps/"))
  (org-id-locations-file (concat minemacs-cache-dir "org/id-locations.el"))
  (org-auto-align-tags nil)
  (org-edit-src-content-indentation 0) ; do not indent the content of src blocks
  (org-ellipsis " ↩")
  (org-export-in-background t) ; run export processes in external emacs process
  (org-export-with-broken-links 'mark) ; Do not rise error on broken links, but mark them in the output file
  (org-export-with-smart-quotes t) ; convert "this" to « this »
  (org-export-with-sub-superscripts '{}) ; Only explicit _{} ^{} are interpreted as sub/superscripts
  (org-fold-catch-invisible-edits 'smart) ; try not to accidentally do weird stuff in invisible regions
  (org-fontify-quote-and-verse-blocks t)
  (org-hide-emphasis-markers t)
  (org-highlight-latex-and-related '(native latex script entities))
  (org-insert-heading-respect-content t)
  (org-list-allow-alphabetical t) ; have a. A. a) A) list bullets
  (org-log-done 'time) ; having the time an item is done sounds convenient
  (org-pretty-entities t)
  (org-return-follows-link t) ; RET follows link
  (org-special-ctrl-a/e t)
  (org-tags-column 0)
  (org-use-property-inheritance t) ; it's convenient to have properties inherited
  (org-use-sub-superscripts '{}) ; Do the same when rendering the Org buffer
  (org-todo-keywords
   '((sequence
      "TODO(t)"  ; A task that needs to be done
      "PROJ(p)"  ; A project, which usually contains other tasks
      "LOOP(r)"  ; A recurring task
      "STRT(s)"  ; A task that is in progress
      "WAIT(w)"  ; Something external is holding up this task
      "HOLD(h)"  ; This task is paused/on hold because of me
      "IDEA(i)"  ; An unconfirmed and unapproved task or notion
      "|"
      "DONE(d)"  ; Task successfully completed
      "KILL(k)") ; Task was cancelled, aborted or is no longer applicable
     (sequence "[ ](T)" "[-](S)" "[?](W)" "|" "[X](D)") ; To do, in progress, on hold, done
     (sequence "|" "OKAY(o)" "YES(y)" "NO(n)")))
  (org-todo-keyword-faces
   '(("[-]"  . +org-todo-active)
     ("STRT" . +org-todo-active)
     ("[?]"  . +org-todo-onhold)
     ("WAIT" . +org-todo-onhold)
     ("HOLD" . +org-todo-onhold)
     ("PROJ" . +org-todo-project)
     ("NO"   . +org-todo-cancel)
     ("KILL" . +org-todo-cancel)))
  :config
  (setq org-export-async-debug minemacs-debug-p) ;; Can be useful!

  (setopt org-babel-load-languages
          (cl-loop
           for lang in '(C R js dot awk sed sql org shell latex julia sqlite octave ; BUG: for some reason, loading `ditaa' causes an error, disabled for now
                           maxima eshell scheme python fortran gnuplot plantuml makefile)
           collect (cons lang t)))

  (with-eval-after-load 'org-src
    (+alist-set "dot" 'graphviz-dot org-src-lang-modes))

  (with-eval-after-load 'plantuml-mode
    (setq org-plantuml-jar-path plantuml-jar-path
          org-plantuml-exec-mode plantuml-default-exec-mode
          org-plantuml-executable-path plantuml-executable-path))

  ;; From Doom Emacs
  (with-no-warnings
    (custom-declare-face '+org-todo-active  '((t (:inherit (bold font-lock-constant-face org-todo)))) "")
    (custom-declare-face '+org-todo-project '((t (:inherit (bold font-lock-doc-face org-todo)))) "")
    (custom-declare-face '+org-todo-onhold  '((t (:inherit (bold warning org-todo)))) "")
    (custom-declare-face '+org-todo-cancel  '((t (:inherit (bold error org-todo)))) "")))

(use-package org-agenda
  :custom
  (org-agenda-tags-column 0))

(use-package ox-latex
  :custom
  (org-latex-src-block-backend 'engraved)
  (org-latex-prefer-user-labels t)
  (org-latex-tables-booktabs t)
  (org-latex-pdf-process
   '("latexmk -c -bibtex-cond1 %f" ; ensure cleaning ".bbl" files
     "latexmk -f -pdf -%latex -shell-escape -interaction=nonstopmode -output-directory=%o %f"))) ; add "-shell-escape"

(use-package ox
  :config
  (mapc #'require '(ox-odt ox-beamer ox-koma-letter)))

(use-package oc
  :custom
  (org-cite-export-processors '((latex biblatex) (t csl)))
  (org-support-shift-select t)
  :config
  (mapc #'require '(oc-csl oc-natbib oc-biblatex)))

(use-package ob-ditaa
  :custom
  ;; Automatically download the latest version of Ditaa
  (org-ditaa-jar-path (+github-download-release "stathissideris/ditaa" "ditaa-.*-standalone.jar" nil :ver "v0.11.0")))

(use-package ol-man
  :after ol
  :demand)

(use-package electric
  :config
  ;; Electric indent on delete and enter
  (setq-default electric-indent-chars '(?\n ?\^?))

  (defvar-local +electric-indent-words nil
    "The list of electric words.
Typing these will trigger reindentation of the current line.")

  ;; Electric indent at Bash/Sh keywords, extracted from the grammar
  (+setq-hook! (sh-mode bash-ts-mode)
    +electric-indent-words (seq-uniq (flatten-list (alist-get :smie-closer-alist sh-smie-sh-grammar))))

  (defun +electric-indent-char-fn (_c) ; Inspired by Doom Emacs
    (when (and (eolp) +electric-indent-words)
      (save-excursion
        (backward-word)
        (looking-at-p (concat "\\<" (regexp-opt +electric-indent-words))))))
  (add-hook 'electric-indent-functions #'+electric-indent-char-fn))

(use-package elec-pair
  :hook (minemacs-after-startup . electric-pair-mode))

(use-package ediff
  :hook (ediff-before-setup . +ediff--save-window-config-h)
  :custom
  (ediff-split-window-function #'split-window-horizontally) ; Split horizontally
  (ediff-window-setup-function #'ediff-setup-windows-plain) ; Setup all windows in one frame
  :config
  (defvar +ediff--saved-window-config nil)

  ;; Save the current window configuration (hooked to `ediff-before-setup-hook')
  (defun +ediff--save-window-config-h ()
    (setq +ediff--saved-window-config (current-window-configuration)))

  ;; Restore the saved window configuration on quit or suspend
  (defun +ediff--restore-window-config-h ()
    (when (window-configuration-p +ediff--saved-window-config)
      (set-window-configuration +ediff--saved-window-config)))

  (dolist (hook '(ediff-quit-hook ediff-suspend-hook))
    (add-hook hook #'+ediff--restore-window-config-h 99)))

(use-package octave
  :mode ("\\.m\\'" . octave-mode)) ; `octave-maybe-mode' only works when the file starts with `function', not generic enough

(use-package bookmark
  :custom
  (bookmark-save-flag 1) ; Save the bookmarks every time a bookmark is made
  :bind (("<left-fringe> <double-mouse-1>" . +bookmark-set-at-mouse)) ; double click on fringe creates a bookmark
  :config
  (push bookmark-default-file +first-file-hook-ignore-list)
  (+ignore-root bookmark-default-file))

(use-package recentf
  :custom
  (recentf-max-saved-items 500) ; Increase the maximum number of saved items (def. 20)
  (recentf-case-fold-search t) ; Ignore case when searching recentf files
  (recentf-save-file (concat minemacs-local-dir "recentf")) ; Needed, otherwise Emacs will still create a file under "~/.emacs.d"
  (recentf-exclude ; Exclude some files from being remembered by recentf
   `(,(rx (or "/elfeed-db/" "/eln-cache/" "/cache/" "/.maildir/" "/.cache/"))
     ,(rx bol "/tmp/")))
  :init
  ;; Enable `recentf-mode' to remember recent files
  (+shutup! (recentf-mode 1))
  :config
  (add-to-list 'recentf-exclude +serialized-symbols-directory))

(use-package url
  :custom
  (url-cookie-file (concat minemacs-local-dir "url/cookie.el"))
  (url-history-file (concat minemacs-local-dir "url/history.el")))

(use-package webjump
  :custom
  (webjump-sites
   '(("Emacs Wiki"    . [simple-query "www.emacswiki.org" "www.emacswiki.org/cgi-bin/wiki/" ""])
     ("DuckDuckGo"    . [simple-query "duckduckgo.com" "duckduckgo.com/?q=" ""])
     ("Qwant"         . [simple-query "www.qwant.com" "www.qwant.com/?q=" ""])
     ("Ecosia"        . [simple-query "www.ecosia.org" "www.ecosia.org/search?q=" ""])
     ("Brave"         . [simple-query "search.brave.com" "search.brave.com/search?q=" ""])
     ("Bing"          . [simple-query "www.bing.com" "www.bing.com/search?q=" ""])
     ("Yahoo"         . [simple-query "www.yahoo.com" "search.yahoo.com/search?p=" ""])
     ("Google"        . [simple-query "www.google.com" "www.google.com/search?q=" ""])
     ("Google Maps"   . [simple-query "www.google.com" "www.google.com/maps?q=" ""])
     ("Google Images" . [simple-query "www.google.com" "www.google.com/images?q=" ""])
     ("Google Groups" . [simple-query "groups.google.com" "groups.google.com/groups?q=" ""])
     ("StackOverflow" . [simple-query "stackoverflow.com" "stackoverflow.com/search?q=" ""])
     ("GitHub Repo"   . [simple-query "github.com" "github.com/search?type=repositories&q=" ""])
     ("GitHub Code"   . [simple-query "github.com" "github.com/search?type=code&q=" ""])
     ("WolframAlpha"  . [simple-query "wolframalpha.com" "wolframalpha.com/input/?i=" ""])
     ("MDN"           . [simple-query "developer.mozilla.org" "developer.mozilla.org/search?q=" ""])
     ("Youtube"       . [simple-query "www.youtube.com" "www.youtube.com/results?search_query=" ""])
     ("Reddit"        . [simple-query "www.reddit.com" "www.reddit.com/search/?q=" ""])
     ("Wikipedia"     . [simple-query "wikipedia.org" "wikipedia.org/wiki/" ""]))))

(use-package time-stamp
  :hook (before-save . time-stamp) ; Update time stamp (if available) before saving a file.
  :custom
  (time-stamp-active t) ; Do enable time-stamps
  (time-stamp-line-limit 12) ; Check the first 12 buffer lines for Time-stamp: <>
  (time-stamp-format "%04Y-%02m-%02d %02H:%02M:%02S")) ; Timestamp format

(use-package whitespace
  :init
  (when (fboundp 'whitespace-page-delimiters-mode) ; Emacs 31+
    (dolist (hook '(prog-mode-hook text-mode-hook conf-mode-hook))
      (add-hook hook #'whitespace-page-delimiters-mode))))

(use-package autorevert
  :hook (minemacs-first-file . global-auto-revert-mode) ; Auto load files changed on disk
  :custom
  (global-auto-revert-non-file-buffers t) ; Revert non-file buffers like dired
  (auto-revert-verbose minemacs-verbose-p)
  :config
  ;; HACK: Avoid delays when auto reverting buffers. This is based on saving the
  ;; modification time of the file on save and on buffer switch.
  (defvar-local +auto-revert-buffer-time nil)
  (defun +file-mtime (file)
    (when-let* ((file-attr (and file (file-exists-p file) (file-attributes file))))
      (file-attribute-modification-time file-attr)))

  (defun +auto-revert--save-file-mtime (&rest _args)
    (+log! "Saving modification time for %S" buffer-file-name)
    (setq +auto-revert-buffer-time (+file-mtime buffer-file-name)))

  (add-hook 'after-save-hook #'+auto-revert--save-file-mtime)
  (advice-add 'after-find-file :after #'+auto-revert--save-file-mtime)

  (defun +auto-revert--on-buffer-switch-h (_frame)
    (unless +auto-revert-buffer-time
      (setq +auto-revert-buffer-time (+file-mtime buffer-file-name)))
    (when-let* ((mtime (+file-mtime buffer-file-name)))
      (unless (equal mtime +auto-revert-buffer-time)
        (+log! "File %S modified externally, reverting immediately!" buffer-file-name)
        (+shutup! (revert-buffer t t)))))

  (add-hook 'window-buffer-change-functions #'+auto-revert--on-buffer-switch-h))

(use-package savehist
  :hook (minemacs-lazy . savehist-mode) ; Save history
  :custom
  (savehist-additional-variables ; Save clipboard, marks, macros, etc.
   '(kill-ring register-alist mark-ring global-mark-ring search-ring regexp-search-ring)))

(use-package saveplace
  :hook (minemacs-first-file . save-place-mode) ; Save place in files
  :custom
  (save-place-limit 1000))

(use-package term
  :hook (term-mode . minemacs-reduce-font-size)
  :config
  ;; Kill `term' buffer on exit (reproduce a similar behavior to `shell's
  ;; `shell-kill-buffer-on-exit').
  (advice-add 'term-sentinel :around #'+kill-buffer-after-sentinel-exit))

(use-package executable
  ;; Make scripts (files starting with shebang "#!") executable when saved
  :hook (after-save . executable-make-buffer-file-executable-if-script-p))

(use-package display-line-numbers
  :hook ((prog-mode conf-mode text-mode authinfo-mode) . display-line-numbers-mode) ; Show line numbers
  :custom
  (display-line-numbers-type 'relative)
  (display-line-numbers-width 4) ; Width for line numbers
  (display-line-numbers-widen t)) ; Display absolute line numbers in narrowed regions

(use-package pixel-scroll
  :hook (minemacs-lazy . pixel-scroll-precision-mode)
  :custom
  (pixel-scroll-precision-use-momentum t)) ; Better scrolling on Emacs29+, specially on a touchpad

(use-package mouse
  :hook (minemacs-lazy . context-menu-mode) ; Enable context menu on mouse right click
  :custom
  (mouse-drag-and-drop-region t) ; Enable Drag-and-Drop of regions
  (mouse-drag-and-drop-region-cross-program t)) ; Enable Drag-and-Drop of regions from Emacs to external programs

(use-package mwheel
  :custom
  (mouse-wheel-scroll-amount ; Make mouse scroll a little faster
   '(2 ((shift) . hscroll) ((meta) . nil) ((control meta) . global-text-scale) ((control) . text-scale)))
  (mouse-wheel-scroll-amount-horizontal 2)) ; Make mouse scroll a little faster horizontally

(use-package gnus
  :custom
  (gnus-dribble-directory (+directory-ensure minemacs-local-dir "gnus/dribble/"))
  (gnus-init-file (concat minemacs-config-dir "gnus/init.el"))
  (gnus-startup-file (concat minemacs-config-dir "gnus/newsrc")))

(use-package time
  :hook (minemacs-lazy . display-time-mode) ; Display time in mode-line
  :custom
  (display-time-string-forms '((propertize (concat 24-hours ":" minutes))))) ; Enable time in the mode-line

(use-package calendar
  :custom
  (calendar-date-style 'iso) ; year/month/day
  (calendar-week-start-day 1)) ; Week starts on Monday

(use-package frame
  :hook (minemacs-lazy . window-divider-mode) ; Display divider between windows
  :hook (minemacs-after-startup . undelete-frame-mode)
  :bind ("C-x u" . undelete-frame) ; I use C-_ for `undo'
  :custom
  ;; Set line width for the divider in `window-divider-mode' to 2px
  (window-divider-default-bottom-width 2)
  (window-divider-default-right-width 2))

(use-package repeat
  :after minemacs-lazy
  :init
  (+shutup! (repeat-mode 1))) ; Enable repeat mode, "C-x o then C-x o" becomes "C-x o o"

(use-package server
  :autoload server-running-p
  :init
  ;; When we start in a non-daemon Emacs, we start a server when Emacs is idle.
  (unless (daemonp)
    (+lazy!
     (unless (server-running-p)
       (let ((inhibit-message t))
         (server-start nil t)
         (+info! "Started Emacs daemon in background."))))))

(use-package speedbar ; config from Crafted Emacs
  :custom
  (speedbar-update-flag t) ; Auto-update when the attached frame changes directory
  (speedbar-use-images nil) ; Disable icon images, instead use text
  :config
  ;; File Extensions
  (speedbar-add-supported-extension
   '(;; Classic Lisp Languages
     ".cl" ".el" ".scm" ".lisp"
     ;; Lua/Fennel (Lisp that transpiles to lua)
     ".lua" ".fnl" ".fennel"
     ;; JVM languages (Java, Kotlin, Clojure)
     ".java" ".kt" ".mvn" ".gradle" ".properties" ".clj"
     ;; C/C++
     ".c" ".cpp" ".cc" ".h" ".hh" ".hpp"
     ;; Shell scripts
     ".sh" ".bash"
     ;; Web Languages and Markup/Styling
     ".php" ".js" ".ts" ".html" ".htm" ".css" ".less" ".scss" ".sass"
     ;; Makefile
     "makefile" "MAKEFILE" "Makefile"
     ;; Data formats
     ".json" ".yaml" ".toml"
     ;; Notes and Markup
     ".md" ".markdown" ".org" ".txt" "README")))

(use-package simple
  :init
  (setq-default indent-tabs-mode nil) ; Never mix, use only spaces
  :hook
  (minemacs-lazy . line-number-mode) ; Show line number in mode-line
  (minemacs-lazy . column-number-mode) ; Show column numbers (a.k.a. cursor position) in the mode-line
  (minemacs-lazy . size-indication-mode) ; Display buffer size on mode line
  ((prog-mode conf-mode org-mode) . visual-line-mode) ; Wrap long lines
  :custom
  (kill-do-not-save-duplicates t) ; Filter duplicate entries in kill ring
  (save-interprogram-paste-before-kill t)) ; Save existing clipboard text into the kill ring before replacing it.

(use-package bug-reference
  :hook ((prog-mode conf-mode) . bug-reference-prog-mode)
  :config
  (put 'bug-reference 'face '+goto-addr-url-face))

(use-package goto-addr
  :hook ((prog-mode conf-mode) . goto-address-prog-mode)
  :custom
  (goto-address-url-face '+goto-addr-url-face))

(use-package visual-wrap
  ;; BUG: Enabling this with the current config may result in some weird
  ;; behaviors. For example, when doing `M-;' on a region in `c++-mode', random
  ;; spaces are displayed (kind of overlays). Enabling this on a "emacs -q"
  ;; session didn't reproduce the issue. It seems it is caused by a friction
  ;; with other settings in MinEmacs.
  :disabled
  :when (>= emacs-major-version 30)
  :hook ((prog-mode conf-mode org-mode) . visual-wrap-prefix-mode)) ; Respect indentation whein wrapping long lines

(use-package winner
  :hook (minemacs-lazy . winner-mode)) ; Window layout undo/redo (`winner-undo' / `winner-redo')

(use-package delsel
  :hook (minemacs-lazy . delete-selection-mode)) ; Replace selection after start typing

(use-package mb-depth
  :hook (minemacs-lazy . minibuffer-depth-indicate-mode)) ; Show recursion depth in minibuffer (see `enable-recursive-minibuffers')

(use-package subword
  :hook (minemacs-lazy . global-subword-mode)) ; Global SubWord mode

(use-package minemacs-window
  :demand t
  :custom
  (window-min-width 30)
  (window-min-height 3)
  (even-window-sizes 'height-only)
  (split-window-preferred-direction 'horizontal)
  (switch-to-buffer-in-dedicated-window 'pop)
  :bind (("<f8>" . window-toggle-side-windows))
  :config
  (setopt ; Stolen from Protesilaos Stavrou's configuration
   display-buffer-alist
   `(;; no window
     ("\\`\\*\\(Warnings\\|Compile-Log\\|Org Links\\)\\*\\'"
      (display-buffer-no-window)
      (allow-no-window . t))
     ;; bottom side window
     ("\\*\\(Org \\(Select\\|Note\\)\\|Agenda Commands\\)\\*" ; the `org-capture' key selection and `org-add-log-note'
      (display-buffer-in-side-window)
      (dedicated . t)
      (side . bottom)
      (slot . 0)
      (window-parameters . ((mode-line-format . none))))
     ;; bottom buffer (NOT side window)
     ((or . ((derived-mode . flymake-diagnostics-buffer-mode)
             (derived-mode . flymake-project-diagnostics-mode)
             (derived-mode . messages-buffer-mode)
             (derived-mode . backtrace-mode)))
      (display-buffer-reuse-mode-window display-buffer-at-bottom)
      (mode . ( flymake-diagnostics-buffer-mode flymake-project-diagnostics-mode
                messages-buffer-mode backtrace-mode))
      (window-height . 0.3)
      (dedicated . t)
      (preserve-size . (t . t))
      (body-function . select-window))
     ("\\*Embark Actions\\*"
      (display-buffer-below-selected)
      (window-height . fit-window-to-buffer)
      (window-parameters . ((no-other-window . t)
                            (mode-line-format . none))))
     ("\\*\\(Output\\|Register Preview\\).*"
      (display-buffer-reuse-mode-window display-buffer-at-bottom))
     ;; below current window
     ("\\(\\*Capture\\*\\|CAPTURE-.*\\)"
      (display-buffer-reuse-mode-window display-buffer-below-selected))
     ((derived-mode . reb-mode) ; M-x re-builder
      (display-buffer-reuse-mode-window display-buffer-below-selected)
      (window-height . 4) ; note this is literal lines, not relative
      (dedicated . t)
      (preserve-size . (t . t)))
     ((or . ((derived-mode . occur-mode)
             (derived-mode . grep-mode)
             (derived-mode . Buffer-menu-mode)
             (derived-mode . log-view-mode)
             (derived-mode . help-mode) ; See the hooks for `visual-line-mode'
             "\\*\\(|Buffer List\\|Occur\\|vc-change-log\\|eldoc.*\\).*"
             "\\*\\vc-\\(incoming\\|outgoing\\|git : \\).*"))
      (+window-display-buffer-below-or-pop)
      (body-function . +window-select-fit-size))
     (+window-shell-or-term-p
      (display-buffer-reuse-mode-window display-buffer-at-bottom)
      (mode . (shell-mode eshell-mode comint-mode eat-mode))
      (body-function . +window-select-fit-size))
     ((derived-mode . compilation-mode)
      (display-buffer-reuse-mode-window display-buffer-at-bottom))
     ("\\*\\(Calendar\\|Bookmark Annotation\\|ert\\).*"
      (display-buffer-reuse-mode-window display-buffer-below-selected)
      (mode . (calendar-mode bookmark-edit-annotation-mode ert-results-mode))
      (dedicated . t)
      (window-height . fit-window-to-buffer))
     ("\\*ispell-top-choices\\*.*"
      (display-buffer-below-selected)
      (window-height . fit-window-to-buffer))
     ;; same window
     ((or . ((derived-mode . Man-mode)
             (derived-mode . woman-mode)
             "\\*\\(Man\\|woman\\).*"))
      (display-buffer-same-window)))))

(use-package windmove
  :after minemacs-lazy
  :demand
  :config
  (windmove-default-keybindings 'shift) ; Navigate windows using Shift+Direction
  (defvar-keymap +windmove-keys
    :repeat t ; Make it work with `repeat-mode'
    "k" #'windmove-up
    "j" #'windmove-down
    "h" #'windmove-left
    "l" #'windmove-right)
  (keymap-global-set "C-c w" +windmove-keys))

(use-package grep
  :hook (grep-mode . minemacs-reduce-font-size)
  :custom
  (grep-use-headings t)) ; subdivide grep output into sections, one per file like `rg' (Emacs 30)

(use-package isearch
  :custom
  (isearch-lazy-count t) ; Show the match count (need a non-nil `isearch-lazy-highlight')
  (search-ring-max 200) ; 16 is too little
  (regexp-search-ring-max 200)
  :bind (;; In AZERTY keyboards, the "%" is inserted as "S-ù", so lets bing "ù" to save pressing the shift key
         ("M-ù" . query-replace)
         ("C-M-ù" . query-replace-regexp)
         :map isearch-mode-map
         ("<up>" . isearch-ring-retreat)
         ("<down>" . isearch-ring-advance)
         ("M-i" . +insert-thing-at-point)))

(use-package re-builder
  :commands (+reb-replace-regexp)
  :config
  (defvar-local +reb--points nil "Store point and region bounds before calling `re-builder'.")

  (defun +re-builder-save-state:before-a (&rest _args)
    (setq +reb--points (cons (point) (when (region-active-p) (list (region-beginning) (region-end))))))

  (advice-add 're-builder :before #'+re-builder-save-state:before-a)

  ;; Adapted from: https://karthinks.com/software/bridging-islands-in-emacs-1
  (defun +reb-replace-regexp (&optional delimited backward)
    "Run `query-replace-regexp' with the contents of `re-builder'.
With non-nil optional argument DELIMITED, only replace matches
surrounded by word boundaries."
    (interactive "P")
    (unless (derived-mode-p '(reb-mode reb-lisp-mode))
      (user-error "Not in a `regexp-builder' buffer"))
    (reb-update-regexp)
    (let* ((re (reb-target-value 'reb-regexp))
           (replacement
            (query-replace-read-to
             re
             (concat "Query replace"
                     (if current-prefix-arg
                         (if (eq current-prefix-arg '-) " backward" " word")
                       "")
                     " regexp"
                     (if (with-selected-window reb-target-window (region-active-p)) " in region" ""))
             t)))
      (with-selected-window reb-target-window
        (let ((pnt (car +reb--points))
              (beg (cadr +reb--points))
              (end (caddr +reb--points)))
          ;; replace with (goto-char (match-beginning 0)) if you want
          ;; to control where in the buffer the replacement starts
          ;; with re-builder
          (goto-char pnt)
          (setq +reb--points nil)
          (reb-quit)
          (query-replace-regexp re replacement delimited beg end backward))))))

(use-package hexl
  :autoload (+hexl-buffer-p)
  :init
  (defcustom +hexl-auto-enable t
    "Enable or disable opening suitable files in `hexl-mode'."
    :group 'minemacs-binary
    :type 'boolean)
  ;; Add the fallback predicate near the end, `objdump-disassemble' predicate is
  ;; added with depth 95, so this one gets called only when the file isn't
  ;; recognizable by `objdump-disassemble'
  (add-hook 'magic-fallback-mode-alist '(+hexl-buffer-p . hexl-mode) 96)
  :config
  ;; A predicate for detecting binary files. Inspired by: https://emacs.stackexchange.com/q/10277/37002
  (defun +hexl-binary-buffer-p (&optional buffer)
    "Check if BUFFER contains binary data.
A binary buffer is defined as containing at least one null byte.
Returns either nil, or the position of the first null byte."
    (with-current-buffer (or buffer (current-buffer))
      (save-excursion (goto-char (point-min)) (search-forward (string ?\x00) nil :noerror))))

  (defun +hexl-buffer-p (&optional buffer)
    "Does BUFFER (defaults to the current buffer) should be viewed using `hexl-mode'."
    (and +hexl-auto-enable (not (eq major-mode 'hexl-mode)) (+hexl-binary-buffer-p buffer))))

(use-package eww
  :custom
  (eww-auto-rename-buffer 'title))

(use-package modus-themes
  :custom
  (modus-themes-prompts '(semibold))
  (modus-themes-completions '((selection . (semibold)) (matches . (extrabold underline))))
  :config
  (setq modus-operandi-palette-user
        '((fg-main       "#282b35")
          (bg-main       "#f0f0f0")
          (ultralight    "#cfd6e2")
          (highlight     "#dbe1eb")
          (lowlight      "#e3e7ef")
          (urgent        "#f53137")
          (crucial       "#303db4")
          (focus         "#940B96")
          (strong        "#000000")
          (meek          "#727d97")
          (mild          "#C8CDD8")
          (faint         "#eceff1")
          (black         "#000000")
          (white         "#FFFFFF")
          (red           "#960d36")
          (green         "#00796b")
          (blue          "#30608c")
          (yellow        "#e0a500")
          (orange        "#966e53")
          (aqua          "#278C87")
          (cyan          "#66CCCC")
          (purple        "#833AE6")
          (bg-region     bg-cyan-intense)
          (fg-region     unspecified)
          (keyword       fg-main)
          (builtin       fg-main)
          (function      focus)
          (number        yellow)
          (operator      fg-main)
          (constant      blue)
          (comment       meek)
          (doc           meek)
          (string        meek)
          (docstring     meek)
          (property      fg-main)
          (type          crucial)
          (variable      fg-main))))


(provide 'me-builtin)
;;; me-builtin.el ends here
